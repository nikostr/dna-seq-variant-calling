from snakemake.utils import min_version


min_version("6.10.0")


configfile: "config/config.yaml"


# declare https://github.com/nikostr/read-mapping as a module
module read_mapping:
    snakefile:
        github("nikostr/read-mapping", path="workflow/Snakefile", tag="v0.4")
    config:
        config


# use all rules from https://github.com/nikostr/read-mapping
use rule * from read_mapping as read_mapping_*


include: "rules/common.smk"
include: "rules/calling.smk"
include: "rules/mappability-map.smk"
include: "rules/delly_calling.smk"
include: "rules/cnv_calling.smk"
include: "rules/qc.smk"


rule all:
    input:
        rules.compress_vcf.output,
        "results/qc/multiqc_freebayes_vcf/multiqc.html",
    default_target: True


rule all_bcftools_vcf:
    input:
        rules.concat_bcftools_vcfs.output,
        "results/qc/multiqc_bcftools_vcf/multiqc.html",


rule all_freebayes_gvcf:
    input:
        rules.compress_gvcf.output,
        "results/qc/multiqc_freebayes_gvcf/multiqc.html",


rule all_delly:
    input:
        rules.delly_bcftools_merge.output,
        "results/qc/multiqc_delly/multiqc.html",


rule all_delly_cnv:
    input:
        rules.bcftools_view_cnv.output,
        "results/qc/multiqc_delly_cnv/multiqc.html",
